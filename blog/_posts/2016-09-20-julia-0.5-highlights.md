---
layout: post
title:  Julia 0.5 Highlights
author: Stefan Karpinski
---

Julia 0.5 is a pivotal release. It introduces more transformative features than any release since the first official version. Moreover, several of these features set the stage for even more to come in the [lead up to Julia 1.0](https://www.youtube.com/watch?v=5gXMpbY1kJY). In this post, we'll go through some of the major changes in 0.5, including improvements to functional programming, comprehensions, generators, arrays, strings, the standard library, and tooling.

## Functions

Julia has always been a functional language in the technical sense: functions are values that can be passed to and from other functions (i.e. [higher-order functions](https://en.wikipedia.org/wiki/Higher-order_function)) and there is full support for [lambdas](https://en.wikipedia.org/wiki/Anonymous_function). Before this release, however, anonymous and higher-order functions came with a significant performance cost, and in a language that targets high-performance technical computing, that's a serious limitation. So the Julia standard library and ecosystem have been rife with work-arounds to get the expressiveness of functional programming without the performance problems. But the right solution, of course, is to make functional programming fast – ideally just as fast as the optimal hand-written version of your code would be. In Julia 0.5, it is. And that changes everything.

This change is so important that there will be a separate blog post about it in the coming weeks, explaining how higher-order functions and lambdas have been made so efficient, as well as detailing the kinds of zero-cost abstractions these changes enable in Julia 0.5. But for now, I'll just tease with a little timing comparison. First, some definitions – they're the same in either version of Julia:

    v = rand(10^7);                   # 10 million random values
    double_it_vec(v) = 2v             # vectorized doubling of input
    double_it_map(v) = map(x->2x, v)  # map a lambda over input

Now, a timing comparison in Julia 0.4:

    julia> VERSION
    v"0.4.7"

    julia> mean([@elapsed(double_it_vec(v)) for _=1:100])
    0.024444888209999998

    julia> mean([@elapsed(double_it_map(v)) for _=1:100])
    0.5515606454499999

As you can see, the functional version using `map` is over 22 times slower than the vectorized form that uses specialized generated code for maximal speed. Next, the same comparison in Julia 0.5:

    julia> VERSION
    v"0.5.0"

    julia> mean([@elapsed(double_it_vec(v)) for _=1:100])
    0.024549842180000003

    julia> mean([@elapsed(double_it_map(v)) for _=1:100])
    0.023871925960000002

In Julia 0.5, the version defined in terms of `map` is as fast than the vectorized method (and maybe even faster, which isn't impossible since the lambda is specialized on the value 2). In this case, writing `2v` happens to be more convenient than writing `map(x->2x, v)`, and you can keep using vectorized forms when they're more convenient. However, there are many cases where functional constructs are more general, clearer, or more convenient, and now also efficient.

### Ambiguous methods

One design decision that any multiple dispatch language must make is how to handle dispatch ambiguities: cases where none of the methods applicable to a given set of arguments is more specific than the rest. Suppose, for example, that a generic function, `f`, has the following methods:

    f(a::Int, b::Real) = 1
    f(a::Real, b::Int) = 2

In Julia 0.4 and earlier, the second method definition causes a method ambiguity warning:

    WARNING: New definition
        f(Real, Int64) at none:1
    is ambiguous with:
        f(Int64, Real) at none:1.
    To fix, define
        f(Int64, Int64)
    before the new definition.

This warning is clear and gets right to the point: the case `f(a, b)` where `a` and `b` are of type `Int` (an alias for `Int64` on 64-bit systems) is ambiguous. Evaluating `f(3,4)` calls the first method of `f` – but this behavior is undefined. Giving a warning whenever methods *could* be ambiguous is a fairly conservative choice: it urges people to define a method covering the ambiguous intersection before even defining the methods that overlap. When we decided to emit ambigous method warnings, we hoped that people would avoid ambigous cases and all would be well in the world.

Emiting warnings for method ambiguities turns out to be both too strict and too lenient. It's far too easy for ambiguities to arise when common generic functions serve as extension points across unrelated packages. When many packages extend the same generic functions, it's *very* common for the methods added to have some ambiguous overlap. This happens even when each package has no ambiguities on its own. Worse still, slight changes to one package can introduce ambiguities elsewhere, resulting in the least fun game of whack-a-mole ever. At the same time, the fact that ambiguities *only* cause warnings means that people learn to ignore them, which is annoying at best, and dangerous at worst: it's far too easy for a real problem to be hidden by a barrage of insignificant ambiguity warnings. In particular, on 0.4 and earlier if an ambiguous method is actually called, no error occurs. Instead, one of the possible methods is called, based on the order in which methods were defined – which is essentially arbitrary when they come from different packages. Often called method works – it does apply, after all – but this is clearly not the right thing to do.

The solution is simple: in Julia 0.5 the existence of potential ambiguities is fine, but actually calling an ambiguous method is an immediate error. The method definitions for `f`, which previously triggered a warning, are now silent, but *calling* `f` with two `Int` arguments is a method dispatch error:

    julia> f(3, 4)
    ERROR: MethodError: f(::Int64, ::Int64) is ambiguous. Candidates:
      f(a::Real, b::Int64) at REPL[2]:1
      f(a::Int64, b::Real) at REPL[1]:1
     in eval(::Module, ::Any) at ./boot.jl:231
     in macro expansion at ./REPL.jl:92 [inlined]
     in (::Base.REPL.##1#2{Base.REPL.REPLBackend})() at ./event.jl:46

This improves the experience of using the Julia package ecosystem considerably, while also making Julia safer and more reliable. No more torrent of insignificant ambiguity warnings. No more playing ambiguity whack-a-mole when someone else refactors their code and accidentally introduces ambuities in yours. No more risk that a method call could be silently broken because of ambiguity warnings that we've all learned to ignore.

### Return type annotations

A long-requested feature has been the ability to annotate method definitions with an explicit return type. This aids the clarity of code, serves as self-documentation, helps the compiler reason about code, and ensures that return types are what the programmer intends and expects. In Julia 0.5, you can annotate method definitions with a return type, like so:

    function clip{T<:Real}(x::T, lo::Real, hi::Real)::T
        if x < lo
            return lo
        elseif x > hi
            return hi
        else
            return x
        end
    end

This annotation on this `clip` method has the effect of inserting implicit calls to `x->convert(T, x)` at each return point of the method. It has no effect on any other method of `clip`. In this case, the annotation ensures that this method always returns a value of the same type as `x`, regardless of what the types of `lo` and `hi` are:

    julia> clip(0.5, 1, 2)
    1.0

    julia> clip(1.5, 1, 2)
    1.5

    julia> clip(2.5, 1, 2)
    2.0

You'll note that the annotated return type here is `T`, which is a type parameter of the `clip` method. Not only is that allowed, but the return type can be an arbitrary expression of argument values, type parameters, and values from outer scopes. For example, here is a variation that promotes its arguments:

    function clip2(x::Real, lo::Real, hi::Real)::promote_type(typeof(x), typeof(lo), typeof(hi))
        if x < lo
            return lo
        elseif x > hi
            return hi
        else
            return x
        end
    end

    julia> clip2(2, 1, 3)
    2

    julia> clip2(2, 1, 13//5)
    2//1

    julia> clip2(2.5, 1, 13//5)
    2.5

Return type annotations are a fairly simply syntactic rewrite, but they make it easier to write methods with consistent and predicatble return types. If different branches of your code can lead to slightly different return types, the fix is now as simple as putting a single type annotation on the entire method.

## Comprehensions & Generators

In the section above on functional programming performance, we used an array comprehension to time an expression repeatedly, producing an array of elapsed times, and applying the `mean` function to that array to compute an average elapsed time:

    julia> mean([@elapsed(double_it_map(v)) for _=1:100])
    0.02431330791

But allocating an array of times here is unneccessary: an average can be computed over streamed data, one value at a time, accumulating only the sum and count of values. In other words, this computation could be expressed with constant memory overhead by interleaving the production of values with the code to compute their mean. Prior to Julia 0.5, the most straightforward way to express such an interleaved computation was to manually perform the interleaving (aka "cut and paste"). In Julia 0.5, if you simply omit the square brackets around an array comprehension, you get a *generator expression*, which instead of producing an array of values, can be iterated over, yielding one value at a time. Since the `mean` function works with arbitrary iterable objects – including generators – expressing an interleaved mean computation using constant memory is now as simple as deleting `[` and `]`:

    julia> mean(@elapsed(double_it_map(v)) for _=1:100)
    0.024382122609999998

This avoids allocating a temporary array of elapsed times entirely, instead evaluating the inner expression, `@elapsed(double_it_map(v))`, each time the `mean` function is ready to accept a new value. In this case, the allocation saved is trivial, but it transforms this code from *O(n)* in the number of samples to *O(1)*. It doesn't take much imagination to think of situations where such a reduction in asymtotic memory usage is crucial. The similar syntax between array comprehensions and generator expressions makes it trivial to move back and forth between the two styles of computation as needed.

### Initializing collections

The new generator syntax dovetails particularly nicely with the Julia convention for constructing collections: to make a new collection, you call the constructor with a single iterable argument, which yields the values you want in the new collection. In its simplest form, this looks something like:

    julia> IntSet([1, 4, 9, 16, 25, 36, 49, 64])
    IntSet([1, 4, 9, 16, 25, 36, 49, 64])

In this input expression, an array of integers is passed to the `IntSet` constructor to create an object representing that set of integers, which in this case happen to be small squares. Once constructed, the `IntSet` object no longer refers to the original array of integers. Instead, it uses a bitmask to efficiently store and operate on sets of positive integers. It displays itself as you would construct it from an array of integers, but that's merely for convenience – there's no actual array anymore.

Now, I'm a human (no blogbots here) and I find typing out even short sequences of perfect squares tedious and error prone – despite a math degree, I'm awful at arithmetic. It would be much easier to generate the values to go in this `IntSet` with an array comprehension:

    julia> IntSet([k^2 for k = 1:8])
    IntSet([1, 4, 9, 16, 25, 36, 49, 64])

This comprehension creates the same array of integers that we manually entered in the first case above. Creating an array object is unnecessary, though – it would be even better to generate the desired squares as they are inserted into the `IntSet` being constructed. Which, of course, is precisely what generator expressions allow:

    julia> Set(k^2 for k = 1:8) # no temporary array!
    IntSet([1, 4, 9, 16, 25, 36, 49, 64])

Let's benchmark these two constructions:

    julia> using BenchmarkTools

    julia> @benchmark IntSet([k^2 for k = 1:8])
    BenchmarkTools.Trial:
      samples:          10000
      evals/sample:     772
      time tolerance:   5.00%
      memory tolerance: 1.00%
      memory estimate:  320.00 bytes
      allocs estimate:  5
      minimum time:     163.00 ns (0.00% GC)
      median time:      199.00 ns (0.00% GC)
      mean time:        245.18 ns (12.95% GC)
      maximum time:     5.36 μs (92.47% GC)

    julia> @benchmark IntSet(k^2 for k = 1:8)
    BenchmarkTools.Trial:
      samples:          10000
      evals/sample:     925
      time tolerance:   5.00%
      memory tolerance: 1.00%
      memory estimate:  160.00 bytes
      allocs estimate:  3
      minimum time:     114.00 ns (0.00% GC)
      median time:      139.00 ns (0.00% GC)
      mean time:        165.74 ns (11.48% GC)
      maximum time:     4.82 μs (93.20% GC)

I'm using the `BenchmarkTools` package here instead of hand-rolled timing loops – `BenchmarkTools` has been carefully designed to avoid many of the common pitfalls of benchmarking code and provide sound statistical estimates of how much time and memory code being benchmarked takes to run. As you can see here, the version that allocates a temporary array uses twice as much memory and is 50% slower than the version using a generator expression.

#### Constructing dictionaries

Generator expressions can be used to construct dictionaries as well, and this use case deserves some special attention since it completes a multi-release process of removing the need for special syntax for `Dict` and making user-defined dictionary types first-class citizens in the Julia ecosystem. Dictionaries are now constructed using these two general-purpose syntaxes:

- `Dict(k₁ => v₁, k₂ => v₂)` for fixed-size dictionaries, and
- `Dict(k(i) => v(i) for i = c)` for variably-sized dictionaries.

Crucially, the type of the resulting dictionary object is completely determined

Our story begins in Julia 0.3, where `=>` only existed as part of special syntaxes for constructing `Dict` objects:

- `[k₁ => v₁, k₂ => v₂]` for fixed-size `Dict` literals, and
- `[k(i) => v(i) for i = c]` for `Dict` comprehensions

These syntaxes are reminiscent of other dynamic languages, and seemed natural when we adopted them. In these other languages, however, the dictionary type is among a small set of built-in types, which are deeply integrated into the language and have special syntax to go along with their special status. As Julia's package ecosystem grew, we realized that in this respect, Julia is more like Java or C++ than Python or Lua: the `Dict` type just isn't that special – it happens to be defined in the standard library, but is otherwise perfectly ordinary. More importantly, many packages provide alternative implementations of dictionary-like data structures, and it is common for programmers to swap one dictionary-like type for another one with different properties and tradeoffs. It might make sense, for example, to replace the unsorted hash-table-based `Dict` with a tree-based `SortedDict`, exposing the same dictionary-like interface, but with different costs for operations, and storing keys in sorted order.

In Julia 0.3, since `Dict` had special syntax, using custom dictionary types was problematic. You couldn't just search-and-replace `Dict` with another type name – instead, you had to find all instances of the above special syntaxes. Worse still, there were no syntax for constructing objects of custom dictionary types. Some dictionary types used keyword arguments – but that only works well when the keys are statically known symbols! Others used arrays of two-element tuples, but `SortedDict([(k₁, v₁), (k₂, v₂)])` is hardly what one would call elegant or usable syntax.

In Julia 0.4, `k => v` was made into a standalone syntax for constructing a `Pair` object with key `k` and value `v`.

non-trivial code rewriting. Worse still, the special treatment of the `Dict` type violates one of the fundamental principles around which Julia was designed: that user-defined types should be just as good as built-in ones. Not having a good syntax for user-defined dictionary types in 0.3 made them distinctly second class.

 when a `Dict` is constructed by writing `Dict(). Since `Dict` now uses a syntax available to any data structure, changing dictionary implementations is now a simple matter of search-and-replace. This change further advances one of the fundamental princples around which Julia is designed: that user-defined types are just as first-class as the built-in ones.

User-defined types – if they're supported at all – tend to be significantly second-class in scripting languages when compared to the built-in types. Julia, on the other hand is built from the ground up, on the principle that user-defined types should be every bit as first class as built-in types – from speed to memory to syntax. To that end, in 0.4, `k => v` was made into a standalone syntax for constructing a `Pair` object with key `k` and value `v`. This allowed the special `[k => v]` syntax to be replaced with a more general `Dict(k => v)` syntax. Some motivation may be in order for 

: since scripting langauges focus on a small set of powerful, general-purpose, built-in types, usually including a dictionary type; these built-in types are special in many ways, including having special syntaxes.

In 0.4,  `k => v` was made into a standalone syntax for constructing a `Pair` object with key `k` and value `v`. Previously the `=>` syntax only existed as part of `Dict` construction syntaxes, `[k => v]` and `[k(i) => v(i) for i = c]`. At the same time, we deprecated `[a => b]` as syntax for constructing fixed-size dictionaries in favor of writing `Dict(a => b)`. In Julia 0.5, we complete the transition by replacing the dictionary comprehension special syntax `["*"^k => k for k = 1:10]` with the more general approach of calling the `Dict` constructor with a generator expression that yields pairs of keys and values:

    julia> Dict("*"^k => k for k = 1:10)
    Dict{String,Int64} with 10 entries:
      "**********" => 10
      "***"        => 3
      "*******"    => 7
      "********"   => 8
      "*"          => 1
      "**"         => 2
      "****"       => 4
      "*********"  => 9
      "*****"      => 5
      "******"     => 6

In Julia 0.4, `a => b` was made into a standalone syntax for constructing a `Pair` object with key `a` and value `b` (previously the syntax only existed as part of `Dict` construction syntaxes). Thus, the expression `"*"^k => k for k = 1:10` is now a generator of key-value pairs, and when passed to `Dict`, the constructor iterates through them, inserting each key-value pair into the new dictionary. In Julia 0.6, the now-deprecated `["*"^k => k for k = 1:10]` will stop having a special meaning and will just produce an array of `Pair` values, as it should, absent special handling in the parser.

Some motivation may be in order for a syntax change that makes constructing a dictionary more verbose – even if only a little. In scripting languages, the built-in dictionary type tends to be very special and deeply integrated into the language, typicially including having special syntax. Partly the special syntax must exist because scripting languages don't have a way of talking about types, or requesting construction of an object of a given type – syntax for literals of a fixed set of built-in types serves this purpose. We initially thought that having special syntax for `Dict` construction also made sense in Julia, but in this aspect Julia turns out to be more like Java or C++ than Python or Lua. Julia's `Dict` is just another type – one which happens to be defined in the standard library, but which is otherwise perfectly ordinary. Julia allows users to define their own types and has a sophisticated language for talking about types, including how to construct instances. Moreover, it's common for Julia programmers to swap one associative array implementation for another one with different properties and tradeoffs. It might make sense, for example, to replace the unsorted hash-table-based `Dict` with a tree-based implementation, exposing the same dictionary-like interface, but storing keys in sorted order with different efficiency tradeoffs between dictionary operations. When there is special syntax for the `Dict` type, this kind of change requires non-trivial code rewriting, switching to a fundamentally different syntax. Since `Dict` now uses a syntax available to any data structure, changing dictionary implementations is now a simple matter of search-and-replace. This change further advances one of the fundamental princples around which Julia is designed: that user-defined types are just as first-class as the built-in ones.

### Filtering and nesting

Julia's array comprehensions have always supported some advanced features such as iterating with multiple variables to produce multidimensional array. This release rounds out the feature set of comprehensions, adding two often-requested features: filtering with the `if` keyword and nested generation with multiple uses of the `for` keyword.

Comprehension filtering allows the `for` clause of a comprehension to be followed by an `if` clause, so that the comprehension only produces values where the predicate is true. For example, let's find first names that palindromes – the same spelled forward and backward. If you want to follow along, download [this list of names](http://julialang.org/blog/data/first-names.txt). First, we load the names:

    julia> names = map(chomp, open(readlines, "first-names.txt"))
    4945-element Array{String,1}:
     "Aaren"
     "Aarika"
     "Abagael"
     "Abagail"
     "Abbe"
     "Abbey"
     "Abbi"
     "Abbie"
     "Abby"
     ⋮
     "Zora"
     "Zorah"
     "Zorana"
     "Zorina"
     "Zorine"
     "Zsa Zsa"
     "Zsazsa"
     "Zulema"
     "Zuzana"

We can use a filtered comprehension to find the ones that are equal to themselves reversed, ignoring case:

    julia> [n for n in names if lowercase(n) == reverse(lowercase(n))]
    18-element Array{String,1}:
     "Ada"
     "Adda"
     "Alla"
     "Ana"
     "Anna"
     "Ara"
     "Ardra"
     "Ava"
     "Aviva"
     "Bab"
     "Ede"
     "Elle"
     "Eve"
     "Hannah"
     "Issi"
     "Lil"
     "Nan"
     "Viv"

Of nearly five thousand first names, only eighteen are palindromes – and all of them are female! Who knew? That is some hard-hitting data science.

One of the features Julia's comprehensions have always had, that is not often found in other languages with is the ability to generate multidimensional arrays using comprehensions. We can, for example, generate a 5×5 rational [Hilbert matrix](https://en.wikipedia.org/wiki/Hilbert_matrix) like so:

    julia> H₅ = [1//(i + j - 1) for i = 1:5, j = 1:5]
    5×5 Array{Rational{Int64},2}:
     1//1  1//2  1//3  1//4  1//5
     1//2  1//3  1//4  1//5  1//6
     1//3  1//4  1//5  1//6  1//7
     1//4  1//5  1//6  1//7  1//8
     1//5  1//6  1//7  1//8  1//9

Hilbert matrices are well-known for being very ill-conditioned (i.e. they break all your linear algebra code). Suppose, that instead of this matrix, we want all of the values in the lower-triangular part of `H₅` as a vector. One way to do it on 0.5 is to use a nested comprehension:

    julia> [1//(i + j - 1) for i = 1:5 for j = 1:i]
    15-element Array{Rational{Int64},1}:
     1//1
     1//2
     1//3
     1//3
     1//4
     1//5
     1//4
     1//5
     1//6
     1//7
     1//5
     1//6
     1//7
     1//8
     1//9

You'll note how similar this syntax is to the multidimensional comprehension syntax – concern about potential confusion is one of the reasons it took so long to settle on the design of this feature, but we decided it was too handy to skip. There are two major differences:

1. Instead of separating the iteration range for `i` and `j` with a comma (`,`), they're separated by a second `for` keyword. You can have as many additional `for` clauses as you like.

2. 

### Inference-independence

## Arrays

### Syntax for vectorized function calls with broadcasting and loop fusion

### "Dim sum" slicing

APL-style array slicing.

### Better array views

### Improved custom array type support

## Strings

### Simplification and unification

## Other

- prime and combinatorics functionality moved into packages
- upgrade of LLVM from version 3.3 to version 3.7.1
- improved [ARM] support and initial support for [Power]
- experimental, scalable threading support
- a powerful interactive debugger

[ARM]: https://en.wikipedia.org/wiki/ARM_architecture
[Power]: https://en.wikipedia.org/wiki/Power_Architecture
